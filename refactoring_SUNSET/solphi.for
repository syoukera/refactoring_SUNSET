C
C*******************************************************************
C
      SUBROUTINE SOLPHI
C
C-----THIS SUBROUTINE IS WRITTEN IN SUCH A GENERAL MANNER THAT IT CAN
C-----BE CALLED FOR ANY PARTICULAR PHI-EQUATION.
C
      COMMON         PHI(40,240),PHIOLD(40,240),GAM(40,40),SC(40,40),
     &               SP(40,40),AE(40,40),AW(40,40),AN(40,40),
     &               AS(40,40),SDX(40,40),SDY(40,40)
      DIMENSION U(40,40),V(40,40),PC(40,40),T(40,40),W(40,40),P(40,40)
      EQUIVALENCE    (PHI(1,1),U(1,1)),(PHI(1,41),V(1,1)),
     &               (PHI(1,81),PC(1,1)),(PHI(1,121),T(1,1)),
     &               (PHI(1,161),W(1,1)),(PHI(1,201),P(1,1))
      COMMON/BLOCK0/ NITERT,ITERT,INTPRI,NLUMP,ISCAN,JSCAN,DTIME,
     &               ISOLVE(10),IPRINT(10),
     &               LUMPE(10),LUMPW(10),LUMPN(10),LUMPS(10),
     &               IRMAX,JRMAX,RESMAX
      COMMON/BLOCK1/ NTIMST,ITURB,IRAD,IREAD,NPHI,IPHI,IU,IV,IPC,IT,
     &               IW,IP,NI,NJ,JDIM,IREF,JREF,RENO,GRNO,DIRCOS,PR,
     &               TIME,IFORS(40,40),ALPHA(10),NIM1,NJM1,NIP1,NJP1,
     &               IEND,JEND,ICNTDF
      COMMON/BLOCK2/ X(40),Y(40),XP(40),YP(40),DXP(40),DYP(40),
     &               DXU(40),DYV(40),DX(40),DY(40),DELX(40),DELY(40),
     &               FACX(40),FACY(40),RX(40),RY(40),BIGNO,ZERO
C
      DIMENSION FS(40),ASS(40)
C
      IF(ISOLVE(IPHI).EQ.0) RETURN
C-----INITIALIZE THE MAXIMUM RESIDUAL, RESMAX.
      IF(IPHI.EQ.IPC) RESMAX=0.
C-----SET DX, DY, DELX, DELY, RX AND RY.
      CALL SETDXY
      CALL SETRXY
C-----SET DIFFUSION COEFFICIENTS, GAM.
      IF(IPHI.NE.IPC) CALL SETGAM
C-----SET AS ALONG THE SOUTH-MOST BOUNDARY.
      DO 1100 I=2,IEND
C-----FOR U-CONTROL VOLUME.
      IF(IPHI.NE.IU) GO TO 1200
      VS=0.5*(V(I,1)+V(I+1,1))
      GAMS=0.25*(GAM(I,1)+GAM(I+1,1)+GAM(I,2)+GAM(I+1,2))
      GO TO 1300
C-----FOR V-CONTROL VOLUME.
 1200 IF(IPHI.NE.IV) GO TO 1400
      VS=0.5*(V(I,1)+V(I,2))
      GAMS=GAM(I,2)
      GO TO 1300
C-----FOR SCALAR-CONTROL VOLUME.
 1400 VS=V(I,1)
      GAMS=0.5*(GAM(I,1)+GAM(I,2))
 1300 FS(I)=VS*DX(I)*RY(1)
C-----ONLY FOR THE PRESSURE CORRECTION.
      IF(IPHI.NE.IPC) GO TO 1500
      ASS(I)=DX(I)*RY(1)*SDY(I,1)
      GO TO 1100
C-----FOR THE OTHER TRANSPORT EQUATIONS.
 1500 DS=GAMS*DX(I)/DELY(1)*RY(1)
      A=0.5*FS(I)+DS
      ASS(I)=AMAX1(A,FS(I),0.)
 1100 CONTINUE
C-----ALL SET ALONG THE SOUTH-MOST BOUNDARY. GO ON TO THE NORTH.
C
      DO 1600 J=2,JEND
C-----SET AW ALONG THE WEST-MOST BOUNDARY.
C-----FOR U-CONTROL VOLUME.
      IF(IPHI.NE.IU) GO TO 1700
      UW=0.5*(U(1,J)+U(2,J))
      GAMW=GAM(2,J)
      GO TO 1800
C-----FOR V-CONTROL VOLUME.
 1700 IF(IPHI.NE.IV) GO TO 1900
      UW=0.5*(U(1,J)+U(1,J+1))
      GAMW=0.25*(GAM(1,J)+GAM(1,J+1)+GAM(2,J)+GAM(2,J+1))
      GO TO 1800
C-----FOR SCALAR-CONTROL VOLUME.
 1900 UW=U(1,J)
      GAMW=0.5*(GAM(1,J)+GAM(2,J))
 1800 FW=UW*DY(J)*RX(J)
C-----ONLY FOR THE PRESSURE CORRECTION.
      IF(IPHI.NE.IPC) GO TO 2100
      AWW=DY(J)*RX(J)*SDX(1,J)
      GO TO 2200
C-----FOR THE OTHER TRANSPORT EQUATIONS.
 2100 DW=GAMW*DY(J)/DELX(1)*RX(J)
      A=0.5*FW+DW
      AWW=AMAX1(A,FW,0.)
C-----ALL SET ALONG THE WEST-MOST BOUNDARY. GO ON TO THE EAST.
 2200 DO 1600 I=2,IEND
C-----SET AW AND AS USING VALUES FROM THE PRECEDING OPERATIONS.
      AW(I,J)=AWW
      AS(I,J)=ASS(I)
C-----FOR U-CONTROL VOLUME.
      IF(IPHI.NE.IU) GO TO 2300
      UE=0.5*(U(I,J)+U(I+1,J))
      GAME=GAM(I+1,J)
      VN=0.5*(V(I,J)+V(I+1,J))
      GAMN=0.5*(1.-FACY(J))*(GAM(I,J)+GAM(I+1,J))
     &    +0.5*FACY(J)*(GAM(I,J+1)+GAM(I+1,J+1))
      GO TO 2400
C-----FOR V-CONTROL VOLUME.
 2300 IF(IPHI.NE.IV) GO TO 2500
      UE=0.5*(U(I,J)+U(I,J+1))
      GAME=0.5*(1.-FACX(I))*(GAM(I,J)+GAM(I,J+1))
     &    +0.5*FACX(I)*(GAM(I+1,J)+GAM(I+1,J+1))
      VN=0.5*(V(I,J)+V(I,J+1))
      GAMN=GAM(I,J+1)
      GO TO 2400
C-----FOR SCALAR CONTROL VOLUME.
 2500 UE=U(I,J)
      GAME=(1.-FACX(I))*GAM(I,J)+FACX(I)*GAM(I+1,J)
      VN=V(I,J)
      GAMN=(1.-FACY(J))*GAM(I,J)+FACY(J)*GAM(I,J+1)
 2400 FE=UE*DY(J)*RX(J)
      FN=VN*DX(I)*RY(J)
C-----ONLY FOR THE PRESSURE CORRECTION.
      IF(IPHI.NE.IPC) GO TO 2600
      AE(I,J)=DY(J)*RX(J)*SDX(I,J)
      AN(I,J)=DX(I)*RY(J)*SDY(I,J)
      AWW=AE(I,J)
      ASS(I)=AN(I,J)
      SC(I,J)=FW-FE+FS(I)-FN
      SP(I,J)=ZERO
C-----FIND THE MAXIMUM RESIDUAL AND ITS LOCATION.
      IF(IFORS(I,J).EQ.0) GO TO 2700
      RES=ABS(SC(I,J))
      IF(RES.LE.RESMAX) GO TO 2700
      RESMAX=RES
      IRMAX=I
      JRMAX=J
      GO TO 2700
C-----FOR THE OTHER TRANSPORT EQUATIONS.
 2600 DE=GAME*DY(J)/DELX(I)*RX(J)
      A=-FACX(I)*FE+DE
      AE(I,J)=AMAX1(A,-FE,0.)
      IF(ICNTDF.EQ.1) AE(I,J)=A
      A=(1.-FACX(I))*FE+DE
      AWW=AMAX1(A,FE,0.)
      IF(ICNTDF.EQ.1) AWW=A
      DN=GAMN*DX(I)/DELY(J)*RY(J)
      A=-FACY(J)*FN+DN
      AN(I,J)=AMAX1(A,-FN,0.)
      IF(ICNTDF.EQ.1) AN(I,J)=A
      A=(1.-FACY(J))*FN+DN
      ASS(I)=AMAX1(A,FN,0.)
      IF(ICNTDF.EQ.1) ASS(I)=A
      SC(I,J)=ZERO
C-----STORE THE PRESSURE GRADIENT TERMS FOR U AND V.
      IF(IPHI.EQ.IU) SC(I,J)=DY(J)*RX(J)*(P(I,J)-P(I+1,J))
      IF(IPHI.EQ.IV) SC(I,J)=DX(I)*RX(J)*(P(I,J)-P(I,J+1))
      RES=FE-FW+FN-FS(I)
C-----SET SP TO ENSURE NUMERICAL STABILITY.
      SP(I,J)=-AMAX1(0.,RES)
      IF(ICNTDF.EQ.1) SP(I,J)=-RES
 2700 FW=FE
 1600 FS(I)=FN
C-----ALL A-S ARE SET. EVALUATE SOURCES AND SOLVE MATRICES.
      CALL SOURCE
      CALL SOLMAT
      RETURN
      END