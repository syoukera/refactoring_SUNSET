C******************     PROGRAM SUNSET   ****************************
C                                                                   C
C  A SOLVER FOR UNSTEADY NAVIER-STOKES EQUATIONS, TWO-DIMENSIONAL   C
C    *          *        *      *      *          *                 C
C
C  THE PROGRAM " SUNSET " HAS BEEN DESIGNED FOR THE FLOW AND HEAT   C
C  TRANSFER ANALYSIS OF UNSTEADY INCOMPRESSIBLE FLOWS WITH OR       C
C  WITHOUT SWIRL. THE EQUATIONS OF CONTINUITY, NAVIER-STOKES AND    C
C  ENERGY BALANCE ARE COUPLED AND SOLVED USING THE " SIMPLE "       C
C  ALGORITHM PROPOSED BY DRS. PATANKAR AND SPALDING. FREE, FORCED   C
C  AND COMBINED CONVECTION PROBLEMS CAN BE TREATED UNDER ARBITRARY  C
C  INITIAL, BOUNDARY AND GEOMETRICAL CONDITIONS. A USER MAY COMBINE C
C  IT WITH ANY PARTICULAR HIGH REYNOLDS NUMBER TURBULENCE MODEL TO  C
C  INVESTIGATE TURBULENT FLOW AND HEAT TRANSFER CHARACTERISTICS.    C
C                                                                   C
C********************************************************************
C
      CHARACTER*8 FILE1,FILE2
      COMMON         PHI(40,240),PHIOLD(40,240),GAM(40,40),SC(40,40),
     &               SP(40,40),AE(40,40),AW(40,40),AN(40,40),
     &               AS(40,40),SDX(40,40),SDY(40,40)
      DIMENSION U(40,40),V(40,40),PC(40,40),T(40,40),W(40,40),P(40,40)
      EQUIVALENCE    (PHI(1,1),U(1,1)),(PHI(1,41),V(1,1)),
     &               (PHI(1,81),PC(1,1)),(PHI(1,121),T(1,1)),
     &               (PHI(1,161),W(1,1)),(PHI(1,201),P(1,1))
      COMMON/BLOCK0/ NITERT,ITERT,INTPRI,NLUMP,ISCAN,JSCAN,DTIME,
     &               ISOLVE(10),IPRINT(10),
     &               LUMPE(10),LUMPW(10),LUMPN(10),LUMPS(10),
     &               IRMAX,JRMAX,RESMAX
      COMMON/BLOCK1/ NTIMST,ITURB,IRAD,IREAD,NPHI,IPHI,IU,IV,IPC,IT,
     &               IW,IP,NI,NJ,JDIM,IREF,JREF,RENO,GRNO,DIRCOS,PR,
     &               TIME,IFORS(40,40),ALPHA(10),NIM1,NJM1,NIP1,NJP1,
     &               IEND,JEND,ICNTDF
      COMMON/BLOCK2/ X(40),Y(40),XP(40),YP(40),DXP(40),DYP(40),
     &               DXU(40),DYV(40),DX(40),DY(40),DELX(40),DELY(40),
     &               FACX(40),FACY(40),RX(40),RY(40),BIGNO,ZERO
C
C
C-----PREPARE FOR A START.
      CALL CONVOL
      CALL CONFIG
      CALL INITIA
      TIME=0.
      CALL DATAIN
C-----TYPE OUTPUT AND INPUT FILE NAMES.
      WRITE(6,350)
      READ(5,400) FILE2
      IF(IREAD.NE.1) GO TO 2000
      WRITE(6,300)
      READ(5,400) FILE1
      OPEN(UNIT=1,FILE=FILE1,STATUS='OLD',ACCESS='SEQUENTIAL',
     &     FORM='UNFORMATTED')
C-----IF IREAD = 1, OVERWRITE RESULTS AND GO ON FROM THAT TIME.
      READ(1) NI,NJ,TIME,PHI,PHIOLD,XP,YP
      CLOSE(UNIT=1,STATUS='KEEP')
C-----PRINT CONTROL PARAMETERS AND INITIAL VALUES TO START WITH.
 2000 WRITE(6,100) NTIMST,NITERT,DTIME,RENO,GRNO,PR,ITURB,IRAD,ICNTDF
      WRITE(6,150)
      DO 1000 J=1,NJP1
      JJ=NJP1-J+1
 1000 WRITE(6,200) JJ,(IFORS(I,JJ),I=1,NIP1)
      JJ=0
      WRITE(6,200) JJ,(I,I=1,NIP1)
      CALL PRINTS
C-----START TIME MARCHING.
      DO 1100 ITIMST=1,NTIMST
C-----SKIP BELOW FOR STEADY SOLUTIONS.
      IF(NTIMST.EQ.1) GO TO 1200
C-----ADVANCE TIME BY DTIME.
      TIME=TIME+DTIME
C-----STORE OLD RESULTS IN PHIOLD.
      DO 1300 IPHI=1,NPHI
      J0=(IPHI-1)*JDIM
      DO 1300 J=1,NJP1
      JJ=J0+J
      DO 1300 I=1,NIP1
 1300 PHIOLD(I,JJ)=PHI(I,JJ)
C-----SET BOUNDARY VALUES AT NEW TIME.
      CALL DATAIN
C-----START ITERATION.
 1200 DO 1400 ITERT=1,NITERT
C-----SOLVE EQS. FOR U, V AND PC.
      DO 1500 IPHI=1,3
 1500 CALL SOLPHI
C-----CORRECT U, V AND P.
      CALL CORREC
C-----MONITOR RESIDUAL, TYPICAL U, V AND P VALUES.
      WRITE(6,250) ITIMST,TIME,ITERT,RESMAX,IRMAX,JRMAX,
     &             U(ISCAN,JSCAN),V(ISCAN,JSCAN),P(ISCAN,JSCAN)
      IF(NPHI.LE.3) GO TO 1600
C-----SOLVE EQS. FOR OTHER SCALARS.
      DO 1700 IPHI=4,NPHI
 1700 CALL SOLPHI
C-----UPDATE BOUNDARY VALUES.
 1600 CALL BOUNDS
      CALL DATAIN
C-----PRINT RESULTS EVERY INTPRI ITERATIONS.
 1400 IF(NTIMST.EQ.1.AND.MOD(ITERT,INTPRI).EQ.0) CALL PRINTS
C-----PRINT RESULTS EVERY INTPRI TIME STEPS.
 1100 IF(NTIMST.GT.1.AND.MOD(ITIMST,INTPRI).EQ.0) CALL PRINTS
C-----PRINT RESULTS AT TERMINATION.
      IF(NTIMST.EQ.1.AND.MOD(NITERT,INTPRI).NE.0) CALL PRINTS
      IF(NTIMST.GT.1.AND.MOD(NTIMST,INTPRI).NE.0) CALL PRINTS
C-----STORE RESULTS IN THE OUTPUT FILE.
      OPEN(UNIT=2,FILE=FILE2,ACCESS='SEQUENTIAL',
     &     FORM='UNFORMATTED')
      WRITE(2) NI,NJ,TIME,PHI,PHIOLD,XP,YP
      CLOSE(UNIT=2,STATUS='KEEP')
  100 FORMAT(/10X,36H***** SUNSET ( A SOLVER FOR UNSTEADY,
     & 50H NAVIER-STOKES EQUATIONS, TWO-DIMENSIONAL ) ******
     & //20X,45H MAXIMUM NUMBER OF TIME STEPS, NTIMST ----- =,I4
     & /20X,45H ITERATION STEPS FOR EACH TIME STEP, NITERT =,I4
     & /20X,45H TIME INCREMENT, DTIME -------------------- =,1PE10.3
     & /20X,45H REYNOLDS NUMBER, RENO -------------------- =,E10.3
     & /20X,45H GRASHOF NUMBER, GRNO --------------------- =,E10.3
     & /20X,45H PRANDTL NUMBER, PR ----------------------- =,E10.3
     & /20X,45H ITURB = 1 FOR TURBULENT FLOW, ITURB ------ =,I4
     & /20X,45H IRAD = 1 FOR CYLINDRICAL COORDINATES, IRAD =,I4
     & /20X,45H ICNTDF = 1 FOR CENTRAL DIFFERENCE,  ICNTDF =,I4)
  150 FORMAT(//2X, 38H GEOMETRICAL CONFIGURATION, IFORS(I,J)/)
  200 FORMAT(2X,51I2)
  250 FORMAT(1X,7HITIMST=,I4,1X,5HTIME=,1PE10.3,1X,6HITERT=,I4,1X,
     &       7HRESMAX=,1PE10.3,1X,6HIRMAX=,I3,1X,6HJRMAX=,I3,1X,
     &       6HUSCAN=,1PE10.3,1X,6HVSCAN=,E10.3,1X,6HPSCAN=,E10.3)
  300 FORMAT(1H+,3X,22HTYPE INPUT FILE NAME :)
  350 FORMAT(1H+,3X,22HTYPE OUTPUT FILE NAME:)
  400 FORMAT(A8)
      STOP
      END